plugins {
    id 'java'
    id 'maven'
    id 'signing'
}

group 'com.github.makintsian'
version '0.0.1'

archivesBaseName = "schema-validator"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.1'
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.1'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    implementation group: 'org.dom4j', name: 'dom4j', version: '2.1.3'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.12.3'
}

test {
    useJUnitPlatform()
    maxHeapSize = '1G'
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'SchemaValidator'
                packaging 'jar'
                description 'Validates json and xml paths'
                url 'https://github.com/makintsian/schema-validator'

                scm {
                    connection 'scm:git:git://github.com/makintsian/schema-validator.git'
                    developerConnection 'scm:git:ssh://github.com:makintsian/schema-validator.git'
                    url 'https://github.com/makintsian/schema-validator/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'makintsian'
                        name 'Igor Makintsian'
                        email 'howletttest@gmail.com'
                    }
                }
            }
        }
    }
}